plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.10'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.asciidoctor.jvm.convert' version '4.0.5'
}

group = 'com.ssafy'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	asciidoctorExt
}

repositories {
	mavenCentral()
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
	// Web
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// Spring Security & JWT
	implementation 'org.springframework.boot:spring-boot-starter-security'
	testImplementation 'org.springframework.security:spring-security-test'
	implementation("io.jsonwebtoken:jjwt-api:0.11.5")
	runtimeOnly("io.jsonwebtoken:jjwt-impl:0.11.5")
	runtimeOnly("io.jsonwebtoken:jjwt-jackson:0.11.5")

	// Dotenv
	implementation 'io.github.cdimascio:java-dotenv:5.1.1'

	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:3.0.5'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// Database
	implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.5'
	runtimeOnly 'com.mysql:mysql-connector-j'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// Redis
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'

	// Devtools
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	// Spring Rest Docs
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'

	// mail
	implementation 'org.springframework.boot:spring-boot-starter-mail'
}

tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

tasks.named('asciidoctor') {
	configurations 'asciidoctorExt'
	inputs.dir snippetsDir
	dependsOn test

	doFirst {
		delete file('src/main/resources/static/docs')
	}
}

tasks.register('copyDocument', Copy) {
	dependsOn tasks.named('asciidoctor')

	from file("build/docs/asciidoc")
	into file("src/main/resources/static/docs")
}

tasks.named('build') {
	dependsOn tasks.named('copyDocument')
}
